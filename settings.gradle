plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '1.0.0'
}

rootProject.name = 'mcp_config'

ext.versions = [] as Set

def addDirectory(path) {
    path.eachDir() {
        def relative = rootDir.toPath().relativize(it.toPath()).toFile()
        include relative.path
        project(':' + relative.path).name = it.name
        versions.add(it.name)
    }
}

def root = hasProperty('root') ? getProperty('root').toLowerCase().replace('\\', '/') : 'release'

if ('release'.equals(root) || root.indexOf('/') != -1) {
    addDirectory(new File(rootDir, 'versions/' + root))
} else {
    new File(rootDir, 'versions/' + root + '/').eachDir() {
        addDirectory(it)
    }
}

println('Versions: ' + versions)

