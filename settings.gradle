rootProject.name = 'mcp_config'

ext.versions = [] as Set

void addDirectory(File path) {
    path.eachDir() {
        File relative = rootDir.toPath().relativize(it.toPath()).toFile()
        include relative.path
        project(':' + relative.path).name = it.name
        versions.add(it.name)
    }
}

final String type = hasProperty('type') ? getProperty('type').toLowerCase() : 'release'
switch (type) {
    case 'release':
        addDirectory(new File(rootDir, 'versions/release/'))
        break
    case 'snapshot':
        if (hasProperty('ver')) {
            addDirectory(new File(rootDir, 'versions/snapshot/' + getProperty('ver')))
        } else {
            new File(rootDir, 'versions/snapshot/').eachDir() {
                addDirectory(it)
            }
        }
        break
    case 'pre':
        if (hasProperty('ver')) {
            addDirectory(new File(rootDir, 'versions/pre/' + getProperty('ver')))
        } else {
            new File(rootDir, 'versions/pre/').eachDir() {
                addDirectory(it)
            }
        }
        break
}

println('Versions: ' + versions)

